buildscript {
    ext.kotlin_version = '1.3.41'

    ext.recompile_kex = false
    ext.recompile_kfg = false


    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.9'
}


apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'


group 'org.jetbrains.research'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

intellij {
    plugins = ['org.jetbrains.kotlin:1.3.40-release-IJ2019.1-1']

    version '2019.1.1'
    pluginName 'Liquid Types'
    updateSinceUntilBuild false
}

repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://dl.bintray.com/cbeust/maven' }
    maven { url "https://dl.bintray.com/saloed/z3" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    mavenCentral()
}


task compileKex(type: Exec) {
    workingDir "kex/"
    if (recompile_kex) commandLine "/usr/bin/mvn", "clean", "compile"
    else commandLine "true"
}

task compileKfg(type: Exec) {
    workingDir "kfg/"
    if (recompile_kfg) commandLine "/usr/bin/mvn", "clean", "compile"
    else commandLine "true"
}

dependencies {
    compile project(':annotation')
    compile "com.microsoft:z3:4.8.6"
    compile "com.beust:klaxon:3.0.6"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.0"
//    compile "org.jetbrains.kotlin:kotlin-compiler:$kotlin_version"

    compile "info.leadinglight:jdot:1.0"
    compile "org.ow2.asm:asm-all:5.2"
    compile files([
            "kex/core/target/classes",
            "kex/kfg/target/classes",
            "kex/kex-util/target/classes",
            "kex/kex-annotation-processor/target/classes",
            "kex/kex-test/target/classes"
    ]) {
        builtBy 'compileKex'
    }

    compile files([
            "kfg/target/classes"
    ]) {
        builtBy 'compileKfg'
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

build {
    dependsOn ':annotation:jar'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/'
        }
        resources {
            srcDir 'resources/'
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

runIde.doFirst {
    if (this.properties.get("maxHeapSize") != null) {
        runIde.maxHeapSize = this.properties.get("maxHeapSize")
    }
}
